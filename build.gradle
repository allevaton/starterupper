apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'sonar-runner'
//apply plugin: 'launch4j'

mainClassName = "com.joeylawrance.starterupper.Main"
version = '0.0.1'
description = 'Starter Upper: Quick git project setup for the classroom.'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
	mavenCentral()
	maven {
		// For SimpleValidation and nbstubs
		url 'http://timboudreau.com/builds/plugin/repository/everything/'
	}
	maven {
		// For SimpleValidation dependencies
		url 'http://bits.netbeans.org/maven2/'
	}
}

// http://code.google.com/p/swt-repo/
dependencies {
	compile 'org.imgscalr:imgscalr-lib:4.2'
	compile 'com.google.guava:guava:16.0'
	compile 'org.eclipse.jgit:org.eclipse.jgit:3.2.0.201312181205-r'
	compile 'com.jcraft:jsch:0.1.50'
	compile 'net.sourceforge.htmlunit:htmlunit:2.13'
	compile 'com.timgroup:jgravatar:1.0'
	compile 'com.github.sarxos:webcam-capture:0.3.9'
	compile 'xmlrpc:xmlrpc-helma:1.0'
	compile 'com.miglayout:miglayout-swing:4.2'
	compile 'com.kenai:simplevalidation:1.4'
	compile 'org.netbeans.api:org-openide-util:RELEASE68'
	compile 'org.swinglabs:swingx:1.6.1'
	compile 'org.slf4j:slf4j-simple:1.7.5'
	compile 'commons-net:commons-net:3.3'
	testCompile 'junit:junit:4.11', 'org.hamcrest:hamcrest-all:1.3'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:4.10'
    }
}

jar {
/*
    // This will extract the compile time dependencies and place them in the fat jar
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	    exclude 'META-INF/**'
    }
*/
    manifest {
        attributes(
            'Implementation-Title'   : 'Starter Upper', 
            'Implementation-Version' : version,
            'Created-By'             : 'Joey Lawrance',
            'Main-Class'             : mainClassName,
        )
    }
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
/*
	doFirst {
        configurations.compile.each { println it}
    }
*/
    ext {
        outDir = file("${buildDir}/proguard")
        smallJar = "${outDir}/starter-upper.jar"
    }
    outDir.mkdirs()

    injars jar.archivePath
    injars(configurations.compile, filter: '!META-INF/**,!about.html,!LICENSE.txt')
    outjars smallJar

//    libraryjars "<java.home>/lib/rt.jar" // doesn't work as well as the one below
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars "${System.getProperty('java.home')}/lib/jce.jar" // probably not necessary

    keep "class com.joeylawrance.starterupper.Main { public static void main(java.lang.String[]); }"
    
    keepattributes
    forceprocessing

    //dontpreverify
    dontoptimize
    printusage
    dontwarn
    dontobfuscate
    keepdirectories
    keep "class com.joeylawrance.starterupper.** { @com.google.common.eventbus.Subscribe public *; }"
    keep "class helma.xmlrpc.** {*;}"
    keep "interface helma.xmlrpc.** {*;}"
    // trying to keep only the important stuff
    keep "class com.gargoylesoftware.htmlunit.** {*;}"
    keep "interface com.gargoylesoftware.htmlunit.** {*;}"
    keep "class org.apache.commons.logging.** {*;}"
    keep "interface org.apache.commons.logging.** {*;}"
    keep "class org.slf4j.** {*;}"
    keep "interface org.slf4j.** {*;}"
    keep "class net.sourceforge.htmlunit.corejs.** {*;}"
    keep "class com.github.sarxos.webcam.** {*;}"
    keep "interface com.github.sarxos.webcam.** {*;}"
    keep "class org.bridj.** {*;}"
    keep "interface org.bridj.** {*;}"
    keep "class org.cyberneko.html.xercesbridge.** {*;}"
    keep "interface org.cyberneko.html.xercesbridge.** {*;}"
}